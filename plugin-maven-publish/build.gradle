plugins {
    id("java-gradle-plugin")
    id("signing")
    id("maven-publish")
}

group = "org.example"
version = "0.1-SNAPSHOT"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

gradlePlugin {
    plugins {
        hello {
            id = "${group}.hello"
            implementationClass = "de.marcphilipp.gradle.example.HelloPlugin"
            displayName = "Hello plugin"
            description = "Plugin that can say hello"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
                artifact sourcesJar
                artifact javadocJar
        }
    }
    // afterEvaluate is necessary because java-gradle-plugin
    // creates its publications in an afterEvaluate callback
    afterEvaluate {
        publications {
            withType(MavenPublication) {
                // customize all publications here
                pom {
                    inceptionYear = "2019"
                }
            }
            pluginMaven {
                // customize main publications here
                pom {
                    name = "main artifact"
                }
            }
            helloPluginMarkerMaven {
                // customize marker publications here
                pom {
                    name = "marker artifact"
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = file("build/repo")
        }
    }
}
